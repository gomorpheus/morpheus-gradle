buildscript {
	repositories {
		mavenLocal()
		jcenter()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
	}
}

plugins {
	id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group = 'com.bertramlabs.plugins'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

repositories {
	jcenter()
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'org.apache.httpcomponents:httpclient:4.5.1'
	compile "com.bertramlabs.plugins:morpheus-java-sdk:0.1.0"
	testCompile('org.spockframework:spock-core:0.7-groovy-2.0')
}

publishing {
	publications {
		maven(MavenPublication) {
			artifactId 'morpheus-gradle'
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name 'morpheus-gradle'
					description 'Morpheus Gradle Provisioning Adapter'
					url 'https://github.com/gomorpheus/morpheus-gradle'
					scm {
						url 'https://github.com/gomorpheus/morpheus-gradle'
						connection 'scm:https://bertramdev@github.com/gomorpheus/morpheus-gradle.git'
						developerConnection 'scm:git://github.com/gomorpheus/morpheus-gradle.git'
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'davydotcom'
							name 'David Estes'
							email 'davydotcom@gmail.com'
						}
					}
				}
			}
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}

	repositories {
		maven {
			credentials {
				username project.properties.get('artifactoryPublishUsername')
				password project.properties.get('artifactoryPublishPassword')
			}
			url 'https://repo.grails.org/grails/libs-snapshots-local/'
		}
	}
}

bintray {
	if(project.hasProperty('bintrayUser')) {
		user = bintrayUser
		key = bintrayKey
	}

	publications = ['maven']
	pkg {
		repo = 'gomorpheus'
		userOrg = 'bertramlabs'
		vcsUrl = 'https://github.com/gomorpheus/morpheus-gradle'
		name = 'morpheus-gradle'
		licenses = ['Apache-2.0']
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
main = 'groovy.ui.Console'
classpath = sourceSets.main.runtimeClasspath
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
	website = 'https://www.gomorpheus.com'
	vcsUrl = 'https://github.com/gomorpheus/morpheus-gradle'
	description = 'Morpheus Provisioning Gradle plugin.'
	tags = ['morpheus', 'deployment']

	plugins {
		assetPipelinePlugin {
			id = 'com.bertramlabs.asset-pipeline'
			displayName = 'Morpheus'
		}
	}
	mavenCoordinates {
		groupId = 'com.bertramlabs.plugins'
	}
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
